
S := 256
screensize(S)
pencolor("#ffffff")
clear()

translate(S/2, S/2)

// divides the line into k equal parts
partition := fn(x1, y1, x2, y2, k) {
    res := []

    i := 0
    while (i <= k) {
        t := float(i) / float(k)

        x := x1*(1-t) + x2*t
		y := y1*(1-t) + y2*t

        res = push(res, [x, y])
        i = i + 1
    }
        
    return res
}

rotatePoints := fn(cx, cy, data, degs) {
    angle := radians(degs)
    s := sin(angle)
    c := cos(angle)

    res := []

    i := 0
    while (i < len(data)) {
        x := data[i][0]
        y := data[i][1]

        // translate point back to origin
        x = x - cx
        y = y - cy

        // rotate point
        xnew := x * c - y * s
        ynew := x * s + y * c

        // translate point back:
        x = xnew + cx
        y = ynew + cy

        res = push(res, [x, y])
        i = i + 1
    }
        
    return res
}

// plots a partitioned line rotating by the specified degrees
plotLine := fn(data, degrees) {
    n := len(data)

    saveState()
    rotate(radians(degrees))

    moveTo(data[0][0], data[0][1])
    lineTo(data[n-1][0], data[n-1][1])
    stroke()

    i := 0
    while (i < n) {
        x := data[i][0]
        y := data[i][1]

        point(x, y, 2)
        fill()

        i = i + 1
    }

    restoreState()
}

k := 10

pencolor("#000000")
pensize(1)

lineA := partition(0, -0.4*S, 0, 0, k)
moveTo(lineA[0][0], lineA[0][1])
lineTo(lineA[k][0], lineA[k][1])
stroke()

lineB := rotatePoints(0, 0, lineA, 120)
moveTo(lineB[0][0], lineB[0][1])
lineTo(lineB[k][0], lineB[k][1])
stroke()

lineC := rotatePoints(0, 0, lineA, 240)
moveTo(lineC[0][0], lineC[0][1])
lineTo(lineC[k][0], lineC[k][1])
stroke()


i := 0
while (i < k) {
  j := k - 1 - i

  x1 := lineA[i][0]
  y1 := lineA[i][1]
  moveTo(x1, y1)

  x2 := lineB[j][0]
  y2 := lineB[j][1]
  lineTo(x2, y2)

  x1 = lineB[i][0]
  y1 = lineB[i][1]
  moveTo(x1, y1)

  x2 = lineC[j][0]
  y2 = lineC[j][1]
  lineTo(x2, y2)
  
  x1 = lineC[i][0]
  y1 = lineC[i][1]
  moveTo(x1, y1)

  x2 = lineA[j][0]
  y2 = lineA[j][1]
  lineTo(x2, y2)
  
  stroke()

  i = i + 1
}

snapshot("./examples/stiching-three.png")